
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'checkstyle'

findbugs {
    toolVersion = "3.0.1"
    sourceSets = [sourceSets.main]
    ignoreFailures = true
    reportsDir = file("$project.buildDir/findbugsReports")
    effort = "max"
    showProgress = true
    reportLevel = "high"
    visitors = ["FindSqlInjection", "SwitchFallthrough"]
    omitVisitors = ["FindNonShortCircuit"]
    findbugsTest.enabled = false
}

checkstyle {
  ignoreFailures = true
  checkstyleTest.enabled = false
}

pmd {
  ignoreFailures = true
}

repositories {
  mavenCentral()
}

dependencies {
  compile 'junit:junit:4.12'
}

task cpdMain(dependsOn: 'pmdMain') {
  ext.ignoreFailures = true

  doLast {
    File outDir = new File(reporting.baseDir, 'cpd/')
    // Make sure the output dir exists to prevent a ReportException
    outDir.mkdirs()

    ant.taskdef(name: 'cpd', classname: 'net.sourceforge.pmd.cpd.CPDTask',
            classpath: configurations.pmd.asPath)

    ant.cpd(
        minimumTokenCount: '100',
        format: 'xml',
        outputFile: new File(outDir , 'main.xml')) {
        fileset(dir: projectDir.getPath()) {
              sourceSets.main.java.each { sourceDir ->
                include(name: project.relativePath(sourceDir.getPath()))
            }
          }
    }
  }
}
check.dependsOn cpdMain